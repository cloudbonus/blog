plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "maven-publish"
}

repositories {
    mavenCentral()
}

group = "com.github"
version = "1.0"

ext {
    mapStructVersion = "1.5.5.Final"
    lombokVersion = "1.18.32"
    hibernateVersion = "6.4.4.Final"
}

dependencies {
    implementation(platform("org.springframework:spring-framework-bom:6.1.6"))
    implementation(platform("org.apache.logging.log4j:log4j-bom:2.23.1"))
    implementation(platform("com.fasterxml.jackson:jackson-bom:2.17.0"))
    implementation(platform("org.hibernate.orm:hibernate-platform:${hibernateVersion}"))
    implementation("org.springframework:spring-context")
    implementation("org.springframework:spring-orm")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")
    implementation("com.fasterxml.jackson.core:jackson-databind")
    implementation("org.apache.logging.log4j:log4j-api")
    implementation("org.liquibase:liquibase-core:4.27.0")
    implementation("org.mapstruct:mapstruct:${mapStructVersion}")
    implementation("jakarta.persistence:jakarta.persistence-api")
    runtimeOnly("org.hibernate.orm:hibernate-core")
    runtimeOnly("org.postgresql:postgresql:42.7.3")
    runtimeOnly("org.apache.logging.log4j:log4j-core")
    compileOnly("org.projectlombok:lombok:${lombokVersion}")
    compileOnly("org.hibernate.orm:hibernate-jpamodelgen")
    annotationProcessor("org.projectlombok:lombok:${lombokVersion}")
    annotationProcessor(platform("org.hibernate.orm:hibernate-platform:${hibernateVersion}"))
    annotationProcessor("org.hibernate.orm:hibernate-jpamodelgen")
    annotationProcessor("org.mapstruct:mapstruct-processor:${mapStructVersion}")
    testImplementation(platform("org.junit:junit-bom:5.10.2"))
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation("org.springframework:spring-test")
    testImplementation("org.assertj:assertj-core:3.25.3")
    testRuntimeOnly("com.h2database:h2:2.2.224")
}

sourceSets {
    main.java.srcDirs += 'build/generated/source/apt/main'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

jar {
    manifest {
        attributes["Implementation-Title"] = "blog"
        attributes["Implementation-Version"] = "1.0"
        attributes["Main-Class"] = "com.github.blog.Main"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}