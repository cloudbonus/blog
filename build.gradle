plugins {
    id "war"
}

repositories {
    mavenCentral()
}

group = "com.github"
version = "1.0"

ext {
    mapStructVersion = "1.5.5.Final"
    lombokVersion = "1.18.32"
    hibernateVersion = "6.4.4.Final"
}

dependencies {
    implementation "org.apache.commons:commons-compress:1.26.1"
    implementation(platform("org.springframework:spring-framework-bom:6.1.6"))
    implementation(platform("org.apache.logging.log4j:log4j-bom:2.23.1"))
    implementation(platform("com.fasterxml.jackson:jackson-bom:2.17.0"))
    implementation(platform("org.hibernate.orm:hibernate-platform:${hibernateVersion}"))
    implementation("org.springframework:spring-webmvc")
    implementation("org.springframework:spring-context")
    implementation("org.springframework:spring-orm")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")
    implementation("com.fasterxml.jackson.core:jackson-databind")
    implementation("org.apache.logging.log4j:log4j-api")
    implementation("org.slf4j:slf4j-api:2.0.13")
    implementation("org.liquibase:liquibase-core:4.27.0")
    implementation("org.mapstruct:mapstruct:${mapStructVersion}")
    implementation("jakarta.persistence:jakarta.persistence-api")
    implementation("org.hibernate.orm:hibernate-core")
    runtimeOnly("org.postgresql:postgresql:42.7.3")
    runtimeOnly("org.apache.logging.log4j:log4j-core")
    providedCompile("jakarta.servlet:jakarta.servlet-api:6.0.0")
    compileOnly("org.projectlombok:lombok:${lombokVersion}")
    compileOnly("org.hibernate.orm:hibernate-jpamodelgen")
    annotationProcessor("org.projectlombok:lombok:${lombokVersion}")
    annotationProcessor(platform("org.hibernate.orm:hibernate-platform:${hibernateVersion}"))
    annotationProcessor("org.hibernate.orm:hibernate-jpamodelgen")
    annotationProcessor("org.mapstruct:mapstruct-processor:${mapStructVersion}")
    testImplementation(platform("org.junit:junit-bom:5.10.2"))
    testImplementation(platform("org.testcontainers:testcontainers-bom:1.19.7"))
    testImplementation(platform("org.mockito:mockito-bom:5.11.0"))
    testImplementation("org.apache.logging.log4j:log4j-slf4j2-impl")
    testImplementation("org.springframework:spring-test")
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation("org.mockito:mockito-junit-jupiter")
    testImplementation("org.springframework:spring-test")
    testImplementation("org.assertj:assertj-core:3.25.3")
    testImplementation("org.testcontainers:postgresql")
    testImplementation("org.testcontainers:junit-jupiter")
    testRuntimeOnly("com.jayway.jsonpath:json-path:2.9.0")
}

tasks.named('test') {
    useJUnitPlatform()
    testLogging{
        events("passed", "skipped", "failed")
    }
}